buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.6'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 4
        versionName "1.3"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE")?:debug.storeFile)
            storePassword System.getenv("KEYSTORE_PASSWORD")?:'foo'
            keyAlias System.getenv("KEY_ALIAS")?:'foo'
            keyPassword System.getenv("KEY_PASSWORD")?:'foo'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        // workaround for http://stackoverflow.com/questions/20673625/android-gradle-plugin-0-7-0-duplicate-files-during-packaging-of-apk
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/groovy-release-info.properties'
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.0.0'
    compile 'org.codehaus.groovy:groovy:2.4.3:grooid'
    compile ('org.codehaus.groovy:groovy-json:2.4.3') {
        transitive = false
    }
    compile 'com.koushikdutta.urlimageviewhelper:urlimageviewhelper:1.0.4'
}
